#
# Copyright (C) 1998, 1999, Jonathan S. Shapiro.
# Copyright (C) 2006, Strawberry Development Group.
#
# This file is part of the EROS Operating System.
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2,
# or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
#

# program to generate gen.SaveArea.h from LAYOUT file

BEGIN {
  nfxdef = 0;
  nfpdef = 0;
  nmask = 0;
  active = "";
}

/^BEGIN/ {
  active = $2;
}

/^END/ {
  active = "";
}

/^(mask|fpmask)/ {
  if (active != "PseudoRegs") {
    thismask = (2 ** $4);
    width = $5;

    value = 0;
  
    for (i = 0; i < width; i++) {
      value += thismask;
      thismask *= 2;
    }

    mask[nmask] = sprintf("#define MASK_%s_%-16s  0x%08x\n",
                          $2, $3, value);
    maskshift[nmask] = sprintf("#define SHIFT_%s_%-16s 0x%08x\n",
                               $2, $3, $4);
    nmask++;
  }
}  

/^(fix|misc)/ {
  if (active == "SaveArea") {
    fxdefs[nfxdef] = sprintf("  %-10s %s;\n", $2, $4);
    nfxdef++;
  }
}

/^(fpu)/ {
  if (active == "FloatSaveArea") {
    fpdefs[nfpdef] = sprintf("  %-10s %s;\n", $2, $4);
    nfpdef++;
  }
}

END	{
  printf("#ifndef __GEN_SAVEAREA_H__\n");	
  printf("#define __GEN_SAVEAREA_H__\n\n");	

  printf("/* This file is automatically generated and is hereby\n");
  printf("   placed in the public domain */\n\n");

  printf("\n");
  for (i = 0; i < nmask; i++)
    printf(mask[i]);

  printf("\n");

  for (i = 0; i < nmask; i++)
    printf(maskshift[i]);
  printf("\n");

  printf("typedef struct savearea savearea_t;\n");	
  printf("struct savearea {\n");	

  for (i = 0; i < nfxdef; i++)
    printf(fxdefs[i]);

  printf("};\n\n");

  printf("typedef struct floatsavearea floatsavearea_t;\n");	
  printf("struct floatsavearea {\n");	

  for (i = 0; i < nfpdef; i++)
    printf(fpdefs[i]);

  printf("};\n\n");

  printf("\n#endif /* __GEN_SAVEAREA_H__ */\n");	
}

