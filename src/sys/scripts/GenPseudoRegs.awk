#
# Copyright (C) 1998, 1999, Jonathan S. Shapiro.
#
# This file is part of the EROS Operating System.
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2,
# or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
#

# program to generate fixregs.h from LAYOUT file

BEGIN {
  nfxdef = 0;
  npseudodef = 0;
  npdef = 0;
  nmask = 0;
  active = "";
}

/^arch/ {
  arch = $2;
  gsub(/\"/, "", arch);
}

/^BEGIN/ {
  active = $2;
}

/^END/ {
  active = "";
}

/^(mask|fpmask)/ {
  if (active == "PseudoRegs") {
    thismask = (2 ** $4);
    width = $5;

    value = 0;
  
    for (i = 0; i < width; i++) {
      value += thismask;
      thismask *= 2;
    }

    mask[nmask] = sprintf("#define MASK_%s_%-16s  0x%08xu\n", $2, $3, value);
    maskshift[nmask] = sprintf("#define SHIFT_%s_%-16s 0x%08xu\n", $2, $3, $4);
    nmask++;
  }
}  

/^(pfix|misc)/ {
  if (active == "PseudoRegs") {
    fxdefs[nfxdef] = sprintf("  %-10s %s;\n", $2, $4);
    nfxdef++;
  }
}

/^(fpu)/ {
}

END	{
  printf("#ifndef __PSEUDOREGS_%s_H__\n", arch);	
  printf("#define __PSEUDOREGS_%s_H__\n\n", arch);	

  printf("/* This file is automatically generated and is hereby\n");
  printf("   placed in the public domain */\n\n");

  printf("\n");
  for (i = 0; i < nmask; i++)
    printf(mask[i]);

  printf("\n");
  for (i = 0; i < nmask; i++)
    printf(maskshift[i]);

  printf("\n");

  printf("typedef struct pseudoregs pseudoregs_t;\n");	
  printf("struct pseudoregs {\n");	

  for (i = 0; i < nfxdef; i++)
    printf(fxdefs[i]);

  printf("};\n\n");

  printf("\n#endif /* __PSEUDOREGS_%s_H__ */\n", arch);	
}

